enable cli scripting
disable clip

# @METADATASTART
#@DetailDescriptionStart
#############################################################################
#
# Script        : RADIUSNetloginConfig
# Revision      : 1.1
# Last Updated  : April 4, 2008
#
# Purpose:  This script provides an example of configuring an ExtremeXOS(TM)
#           switch with RADIUS server netlogin information.  Specifically,
#
#           1.  Configure RADIUS netlogin information
#           2.  Configure VLAN for RADIUS server
#           3.  Enable RADIUS netlogin
#
# Note:  The RADIUS Client IP address must be configured in specified 
#        virtual router name (vr-default) for this script to execute 
#        properly.
#
#############################################################################
#@DetailDescriptionEnd

#############################################################################
# RADIUS and Netlogin Variable definitions
#############################################################################


set var clierrormode  $READ("If this script encounters errors, do you wish to abort or ignore? Please enter abort or ignore exactly")

set var ynradiusnetlogin $READ("Configure and enable RADIUS for Network Login? yes or no")

set var ynradsecretnetlogin $READ("Is The RADIUS Server Shared Secret Encrypted yes or no")
 
set var radservernetlogin $READ("RADIUS Server IP Address")


set var radclientnetlogin $READ("RADIUS Client IP Address. Script will error if IP is not in network.")

set var vrname $READ("RADIUS Client IP Address Virtual Router Name vr-default or vr-mgmt")


set var radsecretnetlogin $READ("RADIUS Server Shared Secret Password")


set var netloginorder $READ("Network Login Authentication Order? e.g. dot1x mac or mac dot1x, encapsulate in double quotes e.g. "dot1x mac"")


set var netloginports $READ("Network Login Port List e.g. 1-10 or 1:1-2:21 for chassis or stacks")


set var guestvlan $READ("Network Login GUEST VLAN this must be set!")

# @MetaDataEnd

#############################################################################
# RADIUS NetLogin
#############################################################################

create log entry "Configuring RADIUS Netlogin"

if (!$match($clierrormode,ignore)) then
  configure cli mode scripting ignore-error
  create log entry "CLI mode set for Ignore on Error"
else
  configure cli mode scripting abort-on-error
  create log entry "CLI mode set for Abort on Error"
endif

if (!$match($ynradsecretnetlogin,yes)) then
  configure radius netlogin primary shared-secret encrypted $radsecretnetlogin
  create log entry "Config RADIUS secret encrypted"
else
  configure radius netlogin primary shared-secret $radsecretnetlogin
  create log entry "Config RADIUS secret NOT encrypted"
endif

if (!$match($ynradiusnetlogin,yes)) then
  create log entry "Starting Network Login Configuration"
  create vlan $guestvlan
  configure netlogin vlan $guestvlan
  configure default delete port $netloginports
  enable netlogin dot1x
  enable netlogin mac
  enable netlogin ports $netloginports $netloginorder
  configure netlogin ports $netloginports mode mac-based-vlans
  configure radius netlogin primary server $radservernetlogin client-ip $radclientnetlogin vr $vrname
  enable radius netlogin
  create log entry "RADIUS Network Login Configuration Complete"
else
  create log entry "No RADIUS Netlogin"
endif

disable cli scripting

